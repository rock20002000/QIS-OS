
ENTRY(_entry);

MEMORY {
    /* Totally 16M reserved for bsp */
    boot_ram (rwx): ORIGIN = CONFIG_RAM_START, LENGTH = 0x100
    ram (rwx):      ORIGIN = CONFIG_RAM_START + 0x100, LENGTH = 0xFFFF00
}

SECTIONS {

    .boot : {
        __image_start_addr = .; /* paddr_t */
        KEEP(*(.boot));
    } > boot_ram


    .text : ALIGN(32) {
        *(.text);
        *(.text.*);
    } > ram

    .rodata : ALIGN(32) {
        *(.rodata);
        *(.rodata.*);
        __ro_end_addr = .; /* paddr_t */
    } > ram

    .data : ALIGN(4096) {
        __data_start_addr = .; /* paddr_t */
        *(.data);
        *(.data.*);
    } > ram

    .bss : ALIGN(32) {
        __bss = .;
        *(.bss);
        *(.bss.*);
        __bss_end = .;

        /* CPU variables and boot stacks (paddr_t). */
        . = ALIGN(4096);
        __boot_stack_base = .;
        . += 0x1000 * 16; /* PAGE_SIZE * CPU_NUM_MAX */
        __boot_image_end = .;
    } > ram

    /DISCARD/ :{
        *(*.eh_frame);
        *(.note.gnu*);
    }

}